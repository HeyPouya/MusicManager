apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

//Important library versions :
def rxjava_Version = "2.2.9"
def lifecycle_version = "2.0.0"
def appcompat_version = "1.0.2"
def retrofit_version = "2.5.0"
def dagger_version = "2.23.1"

//Application versioning
ext.versionMajor = 1
ext.versionMinor = 0
ext.versionPatch = 0
ext.versionClassifier = "BETA"
ext.isBeta = true

private Integer generateVersionCode() {
    return ext.versionMajor * 100 + ext.versionMinor * 10 + ext.versionPatch
}

private String generateVersionName() {
    String versionName = "${ext.versionMajor}.${ext.versionMinor}.${ext.versionPatch}"

    if (ext.isBeta) {
        versionName += "-" + ext.versionClassifier
    }
    return versionName

}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "ir.heydarii.musicmanager"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode generateVersionCode()
        versionName generateVersionName()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        targetCompatibility = "8"
        sourceCompatibility = "8"
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    //Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    //Support
    implementation "androidx.appcompat:appcompat:$appcompat_version"
    implementation "androidx.core:core-ktx:$appcompat_version"
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    //Test
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.0.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    //REST
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.14.1'

    //Rx
    implementation "io.reactivex.rxjava2:rxjava:$rxjava_Version"
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'

    //LiveData and ViewModel
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"

    //Dagger
    implementation "com.google.dagger:dagger:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'

    //Logger
    implementation 'com.orhanobut:logger:2.2.0'


}
